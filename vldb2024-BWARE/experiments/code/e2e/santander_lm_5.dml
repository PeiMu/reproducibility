
stats = function(Matrix[Double] P, Matrix[Double] Y, Double split){
	print("Split: " + split)
	Pc = (P > split) 
	[nn, ca_test] = confusionMatrix(Pc + 1, Y+1)
	accuracy = sum(Y == Pc) / nrow(Y)
	print("Accuracy:")
	print(accuracy)
	print("Confusion Matrix: ")
	print(toString(ca_test))
	print("AUC:      " + auc(Y, P))
}

# Frame input
data = read($1)

# Detect and apply schema
sc = detectSchema(data)
data = applySchema(data, sc)

# Transform encode
spec = read($2, data_type="scalar", value_type="string")
[Xt, M] = transformencode(target=data, spec=spec)

# Replace all Nan Values.
X = replace(target=Xt, pattern=NaN, replacement=0);

# Extract X and Y
Y = X[1:nrow(X), 1]
X = X[1:nrow(X), 2:(ncol(X))]

X = cbind(X, X^2, X^3, X^4, X^5)

# pre-process normalize
[X, cmin, cmax] = normalize(X)

# Train model
bias = lmCG(X=X, y=Y, reg = 1e-1, icpt = 2, tol=1e-10, verbose = TRUE)

# Validate model
y_predict_test = lmPredict(X = X, B=bias, ytest=Y, icpt = 2, verbose = TRUE)

stats(y_predict_test, Y, 0.5)
